plugins {
    id 'java'
    id 'war'
    id 'jacoco'
}

group = 'com.ainetdinov'
version = '1.0'

repositories {
    mavenCentral()
}
ext {
    junitVersion = '5.10.0'
    junitParamsVersion = '5.11.0-M2'
    lombokVersion = '1.18.34'
    jakartaVersion = '6.0.0'
    jacksonVersion = '2.17.1'
    apacheVersion = '3.14.0'
    log4jVersion = '2.23.1'
    mockitoVersion = '5.12.0'
    hamcrestVersion = '1.3'
}

sourceCompatibility = '17'
targetCompatibility = '17'

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

dependencies {
    implementation "jakarta.servlet:jakarta.servlet-api:${jakartaVersion}"
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}"
    implementation "org.apache.commons:commons-lang3:${apacheVersion}"
    implementation "org.projectlombok:lombok:${lombokVersion}"
    implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"

    testImplementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
    testImplementation "org.hamcrest:hamcrest-all:${hamcrestVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitParamsVersion}"

    annotationProcessor("org.projectlombok:lombok:${lombokVersion}")

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.9"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir("jacocoHtml")
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, excludes: [
                    "com/ainetdinov/**/Endpoint.class",
                    "com/ainetdinov/**/ScheduleQuery.class",
                    "com/ainetdinov/**/WebConstant.class",
                    "**/model",
                    "**/listener"
            ])
        }))
    }
}